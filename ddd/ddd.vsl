// $Id$
// VSL functions for DDD graph display

// Copyright (C) 1995 Technische Universitaet Braunschweig, Germany.
// Written by Andreas Zeller <zeller@ips.cs.tu-bs.de>.
// 
// This file is part of the DDD Library.
// 
// The DDD Library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Library General Public
// License as published by the Free Software Foundation; either
// version 2 of the License, or (at your option) any later version.
// 
// The DDD Library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
// See the GNU Library General Public License for more details.
// 
// You should have received a copy of the GNU Library General Public
// License along with the DDD Library -- see the file COPYING.LIB.
// If not, write to the Free Software Foundation, Inc.,
// 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
// 
// DDD is the data display debugger.
// For details, see the DDD World-Wide-Web page, 
// `http://www.cs.tu-bs.de/softech/ddd/',
// or send a mail to the DDD developers at `ddd@ips.cs.tu-bs.de'.

// Includes
#include <std.vsl>
#include <fonts.vsl>
#include <colors.vsl>

// Font settings
#pragma replace stdfontfamily
stdfontfamily() = family_typewriter();

// Shadow effects
shadow(box, thickness) =
  box & (square(thickness) | vrule(thickness))
| square(thickness) & hrule(thickness);
shadow(box) = shadow(box, 1);

// Colors
display_color(box)   = color(box, "black", "grey95");
title_color(box)     = color(box, "black");
disabled_color(box)  = color(box, "white", "grey50");
simple_color(box)    = color(box, "black");
text_color(box)      = color(box, "black");
pointer_color(box)   = color(box, "blue4");
struct_color(box)    = color(box, "black");
list_color(box)      = color(box, "black");
array_color(box)     = color(box, "blue4");
reference_color(box) = color(box, "blue4");

// The title
title (disp_nr, name) -> 
  title_color(rm(disp_nr & ": ") & bf(name) & hfill());
title (name) -> 
  title_color(bf(name) & hfill());

// The "disabled" string
disabled () -> 
  disabled_color(vcenter(it("(Disabled)") & hfill()));

// Ordinary values
simple_value (value) -> 
  simple_color(vcenter(rm(value) & hfill()));

// Collapsed ordinary values
collapsed_simple_value () -> 
  simple_color(vcenter(rm("...") & hfill()));

// Info texts: single lines
text_line (line) -> 
  text_color(rm(line) & hfill());

// Multiple lines
text_value (...) -> valign(...);

// Collapsed ordinary values
collapsed_text_value () -> 
  text_color(rm("...") & hfill());

// Ordinary pointers
pointer_value (value) -> 
  pointer_color(vcenter(rm(value) & hfill()));

// Collapsed pointers
collapsed_pointer_value () -> 
  pointer_color(vcenter(rm("...") & hfill()));

// Dereferenced pointers
dereferenced_pointer_value (value) -> 
  pointer_color(vcenter(bf(value)) & hfill());

// Collapsed array
collapsed_array () -> 
  array_color(vcenter(rm("[...]") & hfill()));

// Vertical array
vertical_array (...) -> 
  array_color(frame(indent(vlist(hwhite() | hrule() | hwhite(), ...))));

// Horizontal array
horizontal_array (...) -> 
  array_color(frame(indent(hlist((vwhite(3) & vrule() & vwhite(3)), ...))));

// Struct value
struct_value (...) -> 
  struct_color(frame(indent(valign(...)) & hfill()));

// Collapsed struct
collapsed_struct_value () -> 
  struct_color(vcenter(rm("{...}") & hfill()));

// Struct member name
struct_member_name (name) -> 
  struct_color(rm(name));

// Struct member
struct_member (name, value, name_width) -> 
  vcenter(rm(name) | hspace(name_width)) 
  & vcenter(rm(" = ")) & rm(value) & hfill();

// List value
list_value (...) -> 
  list_color(valign(...) & hfill());

// Collapsed list
collapsed_list_value () -> 
  list_color(vcenter(rm("...") & hfill()));

// List member name
list_member_name (name) -> 
  list_color(rm(name));

// List member
list_member (name, value, name_width) -> 
  vcenter(rm(name) | hspace(name_width)) 
  & vcenter(rm(" = ")) & rm(value) & hfill();

// Reference
reference_value (ref, value) -> 
  reference_color(vcenter(rm(ref & ": ")) & value & hfill());

// Collapsed reference
collapsed_reference_value () -> 
  reference_color(vcenter(rm("...") & hfill()));

// The entire box
display_box (title, value) -> 
  shadow(display_color(frame(title | hrule() | hwhite () | value)));
